# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgname=python2-pillow
pkgver=6.2.2
pkgrel=2
_appname=Pillow
_py3basever=2.7
pkgdesc="Python Imaging Library (PIL) fork."
arch=('x86_64')
url="https://python-pillow.github.io/"
license=('BSD')
depends=('python2' 'freetype2' 'lcms2' 'libraqm' 'libtiff' 'openjpeg2' 'libimagequant' 'libxcb')
optdepends=('libwebp: for webp images'
            'tk: for the ImageTK module'
            'python2-olefile: OLE2 file support'
            'python2-pyqt5: for the ImageQt module')
makedepends=('python2-setuptools' 'libwebp' 'tk')
checkdepends=('python2-pytest')
source=("https://files.pythonhosted.org/packages/source/P/$_appname/$_appname-$pkgver.tar.gz")
md5sums=('46cad14f0044a5ac4b2d801271528893')

prepare() {
  cd "$srcdir/$_appname-$pkgver"
}

build() {
  cd "$_appname-$pkgver"
  python2 setup.py build
}

check() {
  cd "$_appname-$pkgver"

  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-${_py3basever%m}"
  python2 selftest.py
  pytest2
}

package() {
  cd "$_appname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py3basever/" src/libImaging/*.h
}
