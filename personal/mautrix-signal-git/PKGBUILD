# Maintainer: Joe Holden <jwh@zorins.us>

_pkgbase=mautrix-signal
pkgname=${_pkgbase}-git
pkgver=r96.3852c13
pkgrel=3
pkgdesc='mautrix-signal puppeting/relaybot bridge'
arch=('any')
license=(AGPLv3)
depends=(python-sqlalchemy python-ruamel-yaml python-magic-ahupp python-commonmark
  python-aiohttp python-mautrix-git python-cryptg python-cchardet python-aiodns 
  python-brotli python-pillow 'python-asyncpg>=0.21' python-olm python-phonenumbers
  python-pycryptodome python-unpaddedbase64 python-matrix-nio signald-git python-qrcode)
makedepends=('git' 'python-pip')
url='https://github.com/tulir/mautrix-signal'
source=("${_pkgbase}::git+https://github.com/tulir/${_pkgbase}"
        "usr-share.patch"
        "README"
        "mautrix-signal-db-upgrade"
        "sysusers-mautrix-signal.conf"
        "tmpfiles-mautrix-signal.conf"
        "${_pkgbase}.service")
sha256sums=('SKIP'
            'f1216c42b151e7598384bb42843fb8544ba81bc87b7fcfc58d55479099eedcc9'
            '797083657b11728c7e711182f6d161871fc2fcae02ee5e651d7082a5c34625bd'
            '70c8c19728a22919233f6d31080bddf9507d0f25a17d17f56f3d6854647625fb'
            '0b0abf5194d28fc00642fb46a478470d2f6c1f73c0d5d9b2ea8cc383e45e15b9'
            '69ca6c567273282a7694d831c8a39678900792fcc27806d81de20903eaf16c0d'
            'cdfc114983ed330037d6b3ddfa3d45c51cdfa37f27675fb2caacfe970ef656a3')

pkgver() {
  cd "$srcdir/${_pkgbase}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${_pkgbase}"
  git reset --hard
  echo 'Apply patch to follow fileplacement guidelines per `man heir`'
  patch < ../usr-share.patch
  echo 'Applying patch to permit use of newest mautrix version'
  sed -i 's/^mautrix==/mautrix>=/' requirements.txt
}

package() {
  cd "$srcdir/${_pkgbase}"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "$srcdir/"
  install -Dm644 "$srcdir"/README -t "$pkgdir"/etc/synapse/mautrix-signal/
  install -Dm755 "$srcdir"/mautrix-signal-db-upgrade -t "$pkgdir"/usr/bin/
  install -Dm644 ${_pkgbase}.service -t  "${pkgdir}"/usr/lib/systemd/system/
  # install -Dm644 "$srcdir"/sysusers-mautrix-signal.conf "$pkgdir"/usr/lib/sysusers.d/mautrix-signal.conf
  install -Dm644 "$srcdir"/tmpfiles-mautrix-signal.conf "$pkgdir"/usr/lib/tmpfiles.d/mautrix-signal.conf
}
